#!/usr/bin/env perl
use common::sense;
use Net::DBus;
use Net::DBus::Reactor;

sub usage {
	say for
	"Usage: gnome-mpris-keybind [playername]",
	"",
	"Use the last component of the MPRIS2 bus name as <playername>.",
	"If no name specified, 'mpd' will be used by default.",
	;
	return 2;
}

my $player = shift(@ARGV) // "mpd";
exit(usage) if $player =~ /^-/;

my $bus = Net::DBus->session;
my $mp = $bus->get_service("org.mpris.MediaPlayer2.$player");
my $mp_player = $mp->get_object("/org/mpris/MediaPlayer2");
my $gsd = $bus->get_service("org.gnome.SettingsDaemon");
my $gsd_mmkeys = $gsd->get_object("/org/gnome/SettingsDaemon/MediaKeys");
$gsd_mmkeys->connect_to_signal("MediaPlayerKeyPressed", sub {
	my ($app, $key) = @_;
	given ($key) {
		when ("Play") {
			$mp_player->PlayPause();
		}
		when ("Stop") {
			$mp_player->Stop();
		}
		when ("Next") {
			$mp_player->Next();
		}
		when ("Previous") {
			$mp_player->Previous();
		}
	}
});
my $appname = "gnome-mpris-keybind ($player)";
$gsd_mmkeys->GrabMediaPlayerKeys($appname, 0);
my $reactor = Net::DBus::Reactor->main;
$reactor->run;
exit(0);

#!/usr/bin/env perl
# gnome-mpris-keybind - forward media keys in GNOME to a MPRISv2-capable player
#
# Â© 2011 Mantas M. <grawity@gmail.com>
# Released under WTFPL v2 <http://sam.zoy.org/wtfpl/>

use warnings;
use strict;
use feature "say";
use feature "switch";
use Net::DBus;
use Net::DBus::Reactor;

sub usage {
	say for
	"Usage: gnome-mpris-keybind <playername>",
	"",
	"<playername> is the last component of the player's MPRISv2 D-Bus name; for",
	"example, \"mpd\" for \"org.mpris.MediaPlayer2.mpd\".",
	;
	exit 2;
}

my $player_name = shift(@ARGV) // usage();

if ($player_name =~ /^-/) {
	usage();
} elsif ($player_name =~ /^org\.mpris\.MediaPlayer2\.(.+)$/) {
	$player_name = $1;
} elsif ($player_name =~ /^org\.mpris\./) {
	warn "error: MPRIS v1 interface is not supported\n";
	exit(1);
}

my $app_id	= "gnome-mpris-keybind ($player_name)";

my $bus		= Net::DBus->session;
my $mp_player	= $bus->get_service("org.mpris.MediaPlayer2.$player_name")
			->get_object("/org/mpris/MediaPlayer2");
my $gsd_mmkeys	= $bus->get_service("org.gnome.SettingsDaemon")
			->get_object("/org/gnome/SettingsDaemon/MediaKeys");

$gsd_mmkeys->connect_to_signal("MediaPlayerKeyPressed", sub {
	my ($app_id, $key) = @_;
	given ($key) {
		when ("Play") {
			$mp_player->PlayPause();
		}
		when ("Stop") {
			$mp_player->Stop();
		}
		when ("Next") {
			$mp_player->Next();
		}
		when ("Previous") {
			$mp_player->Previous();
		}
	}
});

$gsd_mmkeys->GrabMediaPlayerKeys($app_id, 0);

my $reactor = Net::DBus::Reactor->main;
$reactor->run;
exit(0);

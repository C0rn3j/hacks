#!/usr/bin/env perl
# gnome-mpris-keybind - forward media keys in GNOME to a MPRISv2-capable player
use v5.10;
use warnings;
no if $] >= 5.017011, warnings => qw(experimental::smartmatch);
use strict;
use Net::DBus;
use Net::DBus::Reactor;

BEGIN {
	if (eval {require Nullroute::Lib}) {
		Nullroute::Lib->import(qw(_debug _info _warn _err _die));
	} else {
		our ($warnings, $errors);
		$::arg0 = (split m!/!, $0)[-1];
		$::debug = !!$ENV{DEBUG};
		sub _debug { warn "debug: @_\n" if $::debug; }
		sub _warn  { warn "warning: @_\n"; ++$::warnings; }
		sub _err   { warn "error: @_\n"; ! ++$::errors; }
		sub _die   { _err(@_); exit 1; }
	}
}

sub usage {
	say for
	"Usage: gnome-mpris-keybind <playername>",
	"",
	"<playername> is the last component of the player's MPRISv2 D-Bus name; for",
	"example, \"mpd\" for \"org.mpris.MediaPlayer2.mpd\".";
}

# Main code

my $player_name = shift @ARGV;

for ($player_name) {
	if (!defined $_) {
		_die("missing player name", 2);
	}
	elsif ($_ eq "--help") {
		usage();
		exit;
	}
	elsif (/^org\.mpris\.MediaPlayer2\.(.+)$/) {
		$player_name = $1;
	}
	elsif (/^org\.mpris\./) {
		_die("MPRIS v1 interface is not supported");
	}
}

my $app_id = "gnome-mpris-keybind ($player_name)";

my $bus = Net::DBus->session;

my $player = $bus->get_service("org.mpris.MediaPlayer2.$player_name")
                 ->get_object("/org/mpris/MediaPlayer2");

my $gsd = $bus->get_service("org.gnome.SettingsDaemon")
              ->get_object("/org/gnome/SettingsDaemon/MediaKeys");

$gsd->connect_to_signal("MediaPlayerKeyPressed", sub {
	my ($key_app_id, $key) = @_;

	return if $key_app_id ne $app_id;

	if ($key eq "Play")        { $player->PlayPause(); }
	elsif ($key eq "Stop")     { $player->Stop(); }
	elsif ($key eq "Next")     { $player->Next(); }
	elsif ($key eq "Previous") { $player->Previous(); }
});

$gsd->GrabMediaPlayerKeys($app_id, 0);

Net::DBus::Reactor->main->run;

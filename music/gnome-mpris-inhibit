#!/usr/bin/env perl
use feature "say";
use Net::DBus;
use Net::DBus::Reactor;
use constant {
	INHIBIT_LOGOUT		=> 1<<0,
	INHIBIT_SWITCH_USER	=> 1<<1,
	INHIBIT_SUSPEND		=> 1<<2,
	INHIBIT_IDLE		=> 1<<3,
};

sub usage {
	say for
		"usage: gnome-mpris-inhibit <playername>"
		;
	exit(2);
}

my $player = shift(@ARGV) // usage();

if ($player =~ /^org\.mpris\.MediaPlayer2\.(.+)$/) {
	$player = $1;
} elsif ($player =~ /^org\.mpris\./) {
	warn "error: MPRIS v1 interface is not supported\n";
	exit(1);
}

my $app_id	= "gnome-mpris-inhibit ($player)";
my $top_xid	= 0;
my $reason	= "Media is playing.";
my $flags	= INHIBIT_IDLE;

my $cookie	= 0;
my $old_status;

my $bus = Net::DBus->session;
my $sm_svc = $bus->get_service("org.gnome.SessionManager");
my $sm = $sm_svc->get_object("/org/gnome/SessionManager");
my $mp_svc = $bus->get_service("org.mpris.MediaPlayer2.$player");
my $mp = $mp_svc->get_object("/org/mpris/MediaPlayer2");

sub inhibit {
	$cookie = $sm->Inhibit($app_id, $top_xid, $reason, $flags);
	warn "inhibit ok ($cookie)\n";
}
sub uninhibit {
	$sm->Uninhibit($cookie);
	warn "uninhibit ok\n";
	$cookie = 0;
}

$mp->connect_to_signal("PropertiesChanged", sub {
	my ($iface, $changed, $invalidated) = @_;
	if ($iface eq 'org.mpris.MediaPlayer2.Player') {
		my $new_status = $old_status;

		if (exists $changed->{PlaybackStatus}) {
			$new_status = $changed->{PlaybackStatus};
		} elsif ("PlaybackStatus" ~~ $invalidated) {
			$new_status = $mp->Get("org.mpris.MediaPlayer2.Player",
							"PlaybackStatus");
		}

		if ($new_status eq $old_status) {
			return;
		} elsif ($new_status eq 'Playing') {
			print "status change: $old_status -> $new_status\n";
			inhibit();
		} elsif ($old_status eq 'Playing') {
			print "status change: $old_status -> $new_status\n";
			uninhibit();
		}
		$old_status = $new_status;
	}
});

$old_status = $mp->Get("org.mpris.MediaPlayer2.Player", "PlaybackStatus");
print "status currently: $old_status\n";
if ($old_status eq 'Playing') {
	inhibit();
}

my $reactor = Net::DBus::Reactor->main;
$reactor->run;
exit(0);

#!/usr/bin/env perl
# gnome-mpris-inhibit - suspend GNOME "session idle" check while music is playing
#
# Â© 2012 Mantas M. <grawity@gmail.com>
# Released under WTFPL v2 <http://sam.zoy.org/wtfpl/>

use warnings;
use strict;
use feature "say";
use Net::DBus;
use Net::DBus::Reactor;
use constant {
	INHIBIT_LOGOUT		=> 1<<0,
	INHIBIT_SWITCH_USER	=> 1<<1,
	INHIBIT_SUSPEND		=> 1<<2,
	INHIBIT_IDLE		=> 1<<3,
};

sub usage {
	say for
	"Usage: gnome-mpris-inhibit <playername>",
	"",
	"Prevents system from automatically going into suspend while music is playing.",
	"",
	"<playername> is the last component of the player's MPRISv2 D-Bus name; for",
	"example, \"banshee\" for \"org.mpris.MediaPlayer2.banshee\".",
	;
	exit(2);
}

my $player_name = shift(@ARGV) // usage();

if ($player_name =~ /^-/) {
	usage();
} elsif ($player_name =~ /^org\.mpris\.MediaPlayer2\.(.+)$/) {
	$player_name = $1;
} elsif ($player_name =~ /^org\.mpris\./) {
	warn "error: MPRIS v1 interface is not supported\n";
	exit(1);
}

# Inhibit API settings
my $app_id	= "gnome-mpris-inhibit ($player_name)";
my $top_xid	= 0;
my $reason	= "Media is playing.";
my $flags	= INHIBIT_IDLE;

# MPRIS variables
my $player_bus_name = "org.mpris.MediaPlayer2.$player_name";
my $ih_cookie;
my $old_status;
my $watching;

my $bus;
my $dbus;
my $sessmgr;
my $player;

sub trace {
	my ($msg) = @_;
	say "$app_id: $msg";
}

sub inhibit {
	if ($ih_cookie) {
		warn "$app_id: double inhibit attempted (have cookie $ih_cookie)";
		return;
	}
	$ih_cookie = $sessmgr->Inhibit($app_id, $top_xid, $reason, $flags);
	trace("inhibit ok ($ih_cookie)");
}

sub uninhibit {
	if (!$ih_cookie) {
		warn "$app_id: double uninhibit attempted";
		return;
	}
	$sessmgr->Uninhibit($ih_cookie);
	$ih_cookie = 0;
	trace("uninhibit ok");
}

sub init_watch {
	trace("started watching");

	$sessmgr = $bus->get_service("org.gnome.SessionManager")
			->get_object("/org/gnome/SessionManager");

	$player = $bus->get_service($player_bus_name)
			->get_object("/org/mpris/MediaPlayer2");

	# needed due to Clementine exposing broken/incomplete introspection data,
	# lacking signals in particular
	my $player_prop = $player->as_interface("org.freedesktop.DBus.Properties");

	$player_prop->connect_to_signal("PropertiesChanged", sub {
		my ($iface, $changed, $invalidated) = @_;
		if ($iface eq "org.mpris.MediaPlayer2.Player") {
			my $new_status;
			if ("PlaybackStatus" ~~ $changed) {
				$new_status = $changed->{PlaybackStatus};
			} elsif ("PlaybackStatus" ~~ $invalidated) {
				$new_status = $player->Get("org.mpris.MediaPlayer2.Player",
								"PlaybackStatus");
			} else {
				return;
			}
			trace("status change: $old_status -> $new_status");

			if ($new_status eq $old_status) {
				return;
			} elsif ($new_status eq 'Playing') {
				inhibit();
			} elsif ($old_status eq 'Playing') {
				uninhibit();
			}
			$old_status = $new_status;
		}
	});

	$old_status = $player->Get("org.mpris.MediaPlayer2.Player", "PlaybackStatus");
	trace("status currently: $old_status");
	if ($old_status eq 'Playing') {
		inhibit();
	}

	$watching = 1;
}

$bus = Net::DBus->session;

$dbus = $bus->get_service("org.freedesktop.DBus")
		->get_object("/org/freedesktop/DBus");

$dbus->connect_to_signal("NameOwnerChanged", sub {
	my ($name, $old_owner, $new_owner) = @_;
	if ($name eq $player_bus_name) {
		if ($old_owner eq "" and $new_owner ne "") {
			trace("$name claimed");
			init_watch() if !$watching;
		} elsif ($old_owner ne "" and $new_owner eq "") {
			trace("$name disappeared");
			uninhibit() if $ih_cookie;
			$old_status = 'Stopped';
		}
	}
});

if (defined $bus->get_service_owner($player_bus_name)) {
	init_watch();
} else {
	trace("waiting for $player_bus_name to appear");
}

my $reactor = Net::DBus::Reactor->main;
$reactor->run;

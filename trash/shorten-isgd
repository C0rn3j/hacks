#!/usr/bin/env perl
# simple URL shortener, using is.gd
use warnings;
use strict;

use URI::Escape 'uri_escape';
use LWP::UserAgent;

sub msg_usage {
	print STDERR "Usage: shorten <url>\n";
	return 2;
}

exit msg_usage() if !@ARGV;

my $req = LWP::UserAgent->new;
my $err = 0;
for my $longurl (@ARGV) {
	my $resp = $req->get("http://is.gd/api.php?longurl=".uri_escape($longurl));

	chomp(my $content = $resp->decoded_content);
	if ($resp->code eq '200' and $content =~ /^http:/) {
		print "$content\n";
	} else {
		warn "error: [is.gd] $content\n";
		$err++;
	}
}
exit $err;

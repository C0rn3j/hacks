#!/usr/bin/env perl
use warnings;
use strict;
use Nullroute::Lib;

sub clean {
	my ($root) = @_;
	_debug("cleaning '$root'");
	if (opendir(my $dh, $root)) {
		while (readdir($dh)) {
			my $path = "$root/$_";
			_debug("- child '$path'");
			if ($_ eq "." || $_ eq "..") {
				;
			}
			elsif (rmdir($path)) {
				_info("removed: $path");
			}
			elsif ($!{ENOTDIR}) {
				;
			}
			elsif ($!{ENOTEMPTY}) {
				clean($path);
			}
			else {
				_err("cannot remove '$path': $!");
			}
		}
		closedir($dh);

		_debug("- root '$root'");
		if ($root eq ".") {
			;
		}
		elsif (rmdir($root)) {
			_info("removed: $root");
		}
		elsif ($!{ENOTEMPTY}) {
			;
		}
		else {
			_err("cannot remove '$root': $!");
		}
	} else {
		_err("cannot open '$root': $!");
	}
}

if (@ARGV) {
	clean($_) for @ARGV;
} else {
	clean(".");
}

exit !!$::errors;

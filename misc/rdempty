#!/usr/bin/env perl
use warnings;
use strict;
use Nullroute::Lib;

sub clean {
	my ($root) = @_;

	_debug("cleaning '$root'");

	# recursively remove contents

	if (opendir(my $dh, $root)) {
		while (readdir($dh)) {
			my $path = "$root/$_";
			_debug("- child '$path'");
			if ($_ eq "." || $_ eq "..") {
				;
			}
			elsif (rmdir($path)) {
				_info("removed: $path");
			}
			elsif ($!{ENOTDIR}) {
				;
			}
			elsif ($!{ENOTEMPTY}) {
				clean($path);
			}
			else {
				_err("cannot remove '$path': $!");
			}
		}
		closedir($dh);
	} else {
		_err("cannot open '$root': $!");
	}

	# remove the directory itself

	if ($root eq ".") {
		;
	}
	elsif (rmdir($root)) {
		_info("removed: $root");
	}
	elsif ($!{ENOTEMPTY}) {
		;
	}
	else {
		_err("cannot remove '$root': $!");
	}
}

if (@ARGV) {
	clean($_) for @ARGV;
} else {
	clean(".");
}

exit !!$::errors;

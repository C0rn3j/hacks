#!/usr/bin/env perl
use warnings;
use strict;
use Getopt::Std;

my %opts;

sub usage {
	print STDERR "Usage: urlencode [-Adr] [string]\n";
	print STDERR "\n";
	print STDERR "    -A    encode for mq\n";
	print STDERR "    -d    decode\n";
	print STDERR "    -r    do not print newline\n";
}

sub decode {
	if ($opts{A}) { s/^:// }
	s/%([A-Fa-f0-9]{2})/pack('C', hex($1))/seg;
}

sub encode {
	if ($opts{A}) {
		s/[\x00-\x1F %]/sprintf("%%%02X", ord($&))/seg;
		s/^$|^:/:$&/;
	} else {
		s/[^A-Za-z0-9_.!~*'-]/sprintf("%%%02X", ord($&))/seg;
	}
}

sub pencode {
	s/[^\/A-Za-z0-9_.!~*'-]/sprintf("%%%02X", ord($&))/seg;
}

sub do_things {
	if ($opts{d}) {
		decode;
	} elsif ($opts{p}) {
		pencode;
	} else {
		encode;
	}
	print;
	print "\n" unless $opts{r};
}

getopts('Adpr', \%opts);

if (scalar @ARGV) {
	do_things for @ARGV;
} else {
	while (<STDIN>) {
		chomp unless $opts{r};
		do_things;
	}
}

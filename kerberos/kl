#!/usr/bin/env perl
use warnings;
use strict;
use feature qw(say switch);
use Getopt::Std;

sub usage {
	say for
		"Usage: kl [-c <ccname> | -l]",
		"",
		"\t-c       show contents of given ccache",
		"\t-l       list known ccaches",
		;
	exit 2;
}

sub uniq { my %seen; grep {!$seen{$_}++} @_; }

sub interval {
	my $end = shift;
	my $start = shift // time;
	my ($dif, $s, $m, $h, $d);

	$dif = $end - $start;
	$dif -= $s = $dif % 60; $dif /= 60;
	$dif -= $m = $dif % 60; $dif /= 60;
	$dif -= $h = $dif % 24; $dif /= 24;
	$d = $dif + 0;

	if ($d > 1)	{ "${d} days" }
	elsif ($h > 0)	{ "${h}h ${m}m" }
	elsif ($m > 1)	{ "${m} mins" }
	else		{ "a minute" }
}

sub mangle_name {
	mangle_principal((shift)->{server_name});
}

sub mangle_principal {
	join "/", map {join ".", reverse split /\./, $_} split /\//, lc shift;
}

sub enum_ccaches {
	my @ccaches;

	open(my $proc, "-|", "pklist", "-l")
		or die "'pklist' not found\n";
	while (<$proc>) {
		my @l = split(/\t/, $_);
		given (shift @l) {
			when ("cache") {
				my ($ccache, $princ) = @l;
				push @ccaches, [$ccache, $princ];
			}
		}
	}
	close($proc);

	return @ccaches;
}

sub display_ccache {
	my ($ccache) = @_;

	my @pklist_args;
	my @fields;
	my $cache;
	my $defprinc;
	my $defrealm;
	my @extrealms;
	my %tgtcreds;
	my %creds;
	my $init;
	my $tgt;

	# read tickets from ccache

	if (defined $ccache) {
		push @pklist_args, ("-c", $ccache);
	}
	open(my $proc, "-|", "pklist", @pklist_args)
		or die "Please install 'pklist' to use this tool.\n";

	while (<$proc>) {
		chomp;
		my @l = split(/\t/, $_);
		given (shift @l) {
			when ("cache") {
				($cache, $defprinc) = @l;
				# If I ever decide to merge the 'cache' line formats in pklist.
				if (defined $defprinc) {
					($defrealm) = $defprinc =~ /@([^@]+)$/;
				}
			}
			when ("principal") {
				($defprinc) = @l;
				($defrealm) = $defprinc =~ /@([^@]+)$/;
			}
			when ("CREDENTIALS") {
				@fields = @l;
			}
			when ("ticket") {
				my %tkt = ();
				@tkt{@fields} = @l;
				my ($name, $realm) = $tkt{server_name} =~ /^(.+)@([^@]*)$/;
				push @extrealms, $realm
					unless $realm eq $defrealm;

				if ($name =~ m|^krbtgt/(.+)| && $realm ne '') {
					push @{$tgtcreds{$1}}, \%tkt;
					push @extrealms, $1
						unless $1 eq $defrealm;
					if ($1 eq $realm) {
						$tgt = \%tkt;
					}
					if ($tkt{flags} =~ /I/) {
						$init = \%tkt;
					}
				} else {
					push @{$creds{$realm}}, \%tkt;
				}
			}
		}
	}
	close($proc);

	if (!defined $cache) {
		return 1;
	}

	if ($defrealm eq 'WELLKNOWN:ANONYMOUS') {
		($defrealm) = ($init // $tgt)->{server_name} =~ /^.+@([^@]+)$/;
	}

	# format TGT expiration time

	my $expirystr;
	my $expiry = $init->{expiry_time} || $tgt->{expiry_time};
	if ($expiry <= time) {
		$expirystr = "\e[1;31m" . "expired" . "\e[m";
	} elsif ($expiry <= time+3600) {
		$expirystr = "\e[1;33m" . "expires in ".interval($expiry) . "\e[m";
	} else {
		$expirystr = "expires in ".interval($expiry);
	}

	# output tickets sorted by realm, server name

	say "Credentials for \e[1m${defprinc}\e[m (${expirystr}):";
	say "";

	my @realms = uniq sort {$a eq '' ? 1 :
				$b eq '' ? -1 :
				$a cmp $b} ($defrealm, @extrealms);

	for my $realm (@realms) {
		$tgtcreds{$realm} //= [];
		$creds{$realm} //= [];

		my @creds = (@{$tgtcreds{$realm}},
				sort {mangle_name($a) cmp mangle_name($b)} @{$creds{$realm}});

		my $num_tgt = 0;
		my $num_srv = 0;

		if (!@{$tgtcreds{$realm}}) {
			my ($color, $label);
			if ($realm eq '') {
				$color = "1;34";
				$label = "cross-realm referral tickets";
			} else {
				$color = "35";
				$label = "tickets for '$realm' (no TGT)";
			}

			say " "x4,
				"\e[${color}m",
				$label,
				"\e[m",
				;
		}

		my $COLOR_TGT_LOCAL	= "32";
		my $COLOR_TGT_CROSS	= "33";
		my $COLOR_TGT_DISTANT	= "35";

		for my $tkt (@creds) {
			my $color = "0";
			my $label = "";
			my $width = 0;
			my $istgt = 0;
			my $isinit = 0;

			my ($svname, $svrealm) = $tkt->{server_name} =~ /^(.+)@(.*)$/;

			if ($svname =~ m|^krbtgt/(.+)$| && $svrealm ne '') {
				my $nextrealm = $1;
				$istgt = 1;

				$color = $svrealm eq $nextrealm
					? $COLOR_TGT_LOCAL
					: $svrealm eq $defrealm
					? $COLOR_TGT_CROSS
					: $COLOR_TGT_DISTANT
					;

				$label .= "\e[1;${color}m${svname}\e[m";
				$width += length $svname;

				if ($svrealm eq $nextrealm) {
					$label .= " \e[${color}m(local)\e[m";
					$width += length " (local)";
				} else {
					$label .= " \e[${color}m(via ${svrealm})\e[m";
					$width += length " (via $svrealm)";
				}
				$num_tgt++;
			} else {
				$label .= " "x2;
				$width += 2;

				$width += length $svname;
				$svname =~ s|/([^.]+)\.|/\e[1m$1\e[m.|;
				$label .= $svname;

				if ($tkt->{client_name} ne $defprinc) {
					my ($defname) = ($tkt->{client_name} =~ /^(.+)@/);
					$width += length " for $defname";
					$label .= " \e[36mfor \e[1m$defname\e[m";
				}
				if ($svrealm eq "") {
					$width += length " (referral)";
					$label .= " \e[34m(referral)\e[m";
				}
				$num_srv++;
			}
			
			my $flags = $tkt->{flags};
			my @eflags = ();
			$flags =~ /A/ or push @eflags, "!A";
			$flags =~ s/[AT]//g;
			if ($istgt) {
				$flags =~ s/[P]//g;
			} else {
				$flags =~ s/[FR]//g;
			}
			$flags = join(" ", $flags, @eflags);

			my $expiry = $tkt->{expiry_time} <= time
					? "expired"
					: interval($tkt->{expiry_time});

			my $expiry_c = ($tkt->{expiry_time} <= time
					? "\e[1;31m"
					: $tkt->{expiry_time} <= time+300
					? "\e[33m"
					: "").$expiry."\e[m";

			say " "x4,
				$label,
				" "x(45-$width),
				" ",
				$flags,
				" "x(5-length $flags),
				" ",
				" "x(8-length $expiry),
				$expiry_c,
				;
		}

		if (!$num_srv) {
			say " "x4,
				"  (no service tickets)",
				;
		}
	}

	say "";
	return 0;
}

my %opts;
my $ccache	= undef;
my $list_all	= 1;

getopts('c:lL', \%opts) or usage();
if (exists $opts{c}) {
	$ccache = $opts{c};
	$list_all = 0;
} elsif (exists $opts{l}) {
	$list_all = 1;
} elsif (exists $opts{L}) {
	$list_all = 0;
}

my @visible;

if ($list_all) {
	@visible = map {$_->[0]} sort {$a->[1] cmp $b->[1]} enum_ccaches();
} else {
	@visible = ($ccache);
}

if (@visible) {
	display_ccache($_) for @visible;
} else {
	say "No credentials cache found.";
}

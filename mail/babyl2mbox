#!/usr/bin/env perl
# babyl2mbox - convert Rmail BABYL storage format to mboxrd.
#
# Quick hack intended for one conversion. For a better tool see `b2m` in Emacs.
# Originally written to convert the following, which `formail -B` could not:
# <http://www.ibiblio.org/pub/Linux/docs/old/misc/linux_is_obsolete.txt.z>
#
# BABYL format: <http://git.savannah.gnu.org/cgit/emacs.git/log/etc/BABYL>
# mbox metadata: <http://wiki.dovecot.org/MailboxFormat/mbox>
#
# Released under WTFPL v2 <http://sam.zoy.org/wtfpl/>

use warnings;
use strict;
use feature 'switch';
use POSIX qw(strftime);
use constant TIMEFMT_MBOX => '%a %b %_d %H:%M:%S %Y';

my $count;
my $msgstatus;
my $msgxstatus;
my $reformed;
my $state;
my @mailheader;

sub parse_date {
	my ($str) = @_;

	my %MONTHS = qw(Jan 0 Feb 1 Mar 2 Apr 3 May 4 Jun 5
			Jul 6 Aug 7 Sep 8 Oct 9 Nov 10 Dec 11);

	if (my @m = $str =~ /^(?:(\w+), )?(\d+) (\w+) (\d+) (\d+):(\d+):(\d+) (\w+)$/) {
		my ($wday, $day, $mon, $yr, $hr, $mn, $sec, $tz) = @m;
		if ($yr >= 100) { $yr -= 1900; }
		return ($sec, $mn, $hr, $day, $MONTHS{$mon}, $yr);
	} else {
		warn "unparsed date: $str\n";
	}
}

sub output_header {
	my ($from, $date);
	for (@mailheader) {
		if (/^From: (.+)/) {
			$from = $1;
		}
		elsif (/^Date: (.+)/) {
			$date = $1;
		}
	}

	$from =~ s/\s*\(.+?\)\s*/ /g;
	$from =~ s/^\s+//;
	$from =~ s/\s+$//;

	$date = strftime(TIMEFMT_MBOX, parse_date($date));

	print "From $from $date\n";

	print "$_\n" for @mailheader;
	print "\n";
}

if (!@ARGV and -t 0) {
	warn "Expecting BABYL mail file as stdin.\n";
}

$state = "babyl-options-header";
$count = 0;

while (<STDIN>) {
	chomp;
	if ($state eq "babyl-options-header") {
		if ($_ eq "BABYL OPTIONS:") {
			$state = "babyl-options";
		} else {
			warn "expected BABYL options section at first line\n";
		}
	}
	elsif ($_ eq "\037\014") {
		if ($count++ > 0) {
			print "\n";
		}
		$state = "msg-status";
		@mailheader = ();
		$msgstatus = "";
		$msgxstatus = "";
	}
	elsif (/^\037\s*$/) {
		last;
	}
	elsif ($state eq "babyl-options") {
		if (/^Version: (\d+)/) {
			if ($1 != 5) {
				warn "script not tested with BABYL version $1\n";
			}
		}
	}
	elsif ($state eq "msg-status") {
		# TODO: full label/status parsing
		my ($basic, $user) = split(/,,\s*/, $_, 2);
		my @basic = split(/,\s*/, $basic);
		my @user = split(/,\s*/, $user);
		#$reformed = /^1,/;
		$reformed = int shift(@basic);

		my $seen = 1;
		for (@basic) {
			when ("answered") {
				$msgxstatus .= "A";
			}
			when ("deleted") {
				$msgxstatus .= "D";
			}
			when ("unseen") {
				$seen = 0;
			}
		}
		if ($seen) {
			$msgstatus .= "RO";
		}

		push @mailheader, "Status: $msgstatus" if length($msgstatus);
		push @mailheader, "X-Status: $msgxstatus" if length($msgxstatus);
		push @mailheader, "X-Keywords: @user" if @user;

		$state = "msg-original-header";
	}
	elsif ($state eq "msg-original-header") {
		if ($_ eq '*** EOOH ***') {
			$state = "mail-header";
		} elsif ($reformed) {
			push @mailheader, $_ unless $_ eq "";
		}
	}
	elsif ($state eq "mail-header") {
		if ($_ eq '') {
			output_header;
			$state = "mail-body";
		} elsif (! $reformed) {
			push @mailheader, $_;
		}
	}
	elsif ($state eq "mail-body") {
		if (/^>*From /) {
			s/^/>/;
		}
		print "$_\n";
	}
}

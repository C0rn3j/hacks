#!/usr/bin/env perl
# gnome-inhibit-list - list power saving inhibitors in GNOME
#
# Â© 2012 Mantas M. <grawity@gmail.com>
# Released under WTFPL v2 <http://sam.zoy.org/wtfpl/>

use warnings;
use strict;
use feature "say";
use locale;

use List::Util qw[max];
use Net::DBus;

use constant {
	INHIBIT_LOGOUT		=> 1<<0,
	INHIBIT_SWITCH_USER	=> 1<<1,
	INHIBIT_SUSPEND		=> 1<<2,
	INHIBIT_IDLE		=> 1<<3,
};

sub usage {
	say for
	"Usage: gnome-inhibit-list",
	"",
	"Shows all inhibitors kept by GNOME Session Manager.",
	;
	exit(0);
}

sub maxlength {
	my ($attr, @items) = @_;
	(max map {length $_->{$attr}} @items) // 0;
}

usage if @ARGV;

my $bus	= Net::DBus->session;
my $sms	= $bus->get_service("org.gnome.SessionManager");
my $sm	= $sms->get_object("/org/gnome/SessionManager");

my @inhibitors =
	sort {$a->{app_id} cmp $b->{app_id}}
	map {
		my $ih_path = $_;
		my $ih = $sms->get_object($ih_path);
		my $v = {
			path      => $ih_path,
			app_id    => $ih->GetAppId,
			client_id => eval {$ih->GetClientId} // "(none)",
			flags     => $ih->GetFlags,
			reason    => eval {$ih->GetReason} // "(none)",
			xid       => $ih->GetToplevelXid,
		};

		my @flags = grep {defined} (
			$v->{flags} & INHIBIT_LOGOUT      ? "logout"      : undef,
			$v->{flags} & INHIBIT_SWITCH_USER ? "switch-user" : undef,
			$v->{flags} & INHIBIT_SUSPEND     ? "suspend"     : undef,
			$v->{flags} & INHIBIT_IDLE        ? "idle"        : undef,
			$v->{flags} ? undef : "none",
		);
		$v->{szflags} = join(",", @flags);
		$v;
	}
	@{$sm->GetInhibitors};

my $len_app_id = max(11, maxlength app_id => @inhibitors);
my $len_reason = max(6,  maxlength reason => @inhibitors);
my $len_flags  = max(8,  maxlength szflags => @inhibitors);

printf "%-*s  %-*s  %-*s\n",
	$len_app_id,	"APPLICATION",
	$len_reason,	"REASON",
	$len_flags,	"INHIBITS";

for my $v (@inhibitors) {
	printf "%-*s  %-*s  %-*s\n",
		$len_app_id,	$v->{app_id},
		$len_reason,	$v->{reason},
		$len_flags,	$v->{szflags};
}

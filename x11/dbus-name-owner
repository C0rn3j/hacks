#!/usr/bin/env perl
# dbus-name-owner - display owners of D-Bus names
use warnings;
use strict;
use feature qw(say);
use Getopt::Long;
use Net::DBus;

my $bus;
my $err = 0;
my $activate = 0;
my $quiet = 0;
my $list = 0;

sub usage {
	say for
	"Usage: dbus-name-owner {-y|-e|-a ADDRESS} [-c] [-q] NAME...",
	"",
	"  -y, --system           Use system bus",
	"  -e, --session          Use session bus",
	"  -a, --address=ADDR     Connect to given D-Bus address",
	"  -q, --quiet            Remain silent, just set exit code",
	"  -c, --activate         Try to launch services using DBus activation",
	"  -l, --list             List all owned names (or activatable names with -c)",
	"",
	"Without explicit bus specification, the heuristics implemented by Net::DBus",
	"will be used (session if DBUS_SESSION_BUS_ADDRESS is set, system otherwise).",
	"",
}

GetOptions(
	"h|help"	=> sub { usage(); exit; },
	"y|system"	=> sub { $bus = Net::DBus->system; },
	"e|session"	=> sub { $bus = Net::DBus->session; },
	"a|address=s"	=> sub {
				my ($opt, $addr) = @_;
				$bus = Net::DBus->new($addr);
			},
	"q|quiet"	=> \$quiet,
	"c|activate"	=> \$activate,
	"l|list"	=> \$list,
) or do { usage(); exit 2; };

$bus //= Net::DBus->find;

if ($list) {
	warn "Note: --quiet does not make sense with --list.\n" if $quiet;
	my $obj = $bus->get_bus_object;
	my $names = $activate
			? $obj->ListActivatableNames
			: $obj->ListNames;
	say for sort @$names;
} elsif (@ARGV) {
	for my $name (@ARGV) {
		if ($activate) {
			eval {$bus->get_service($name)} or warn "$@";
		}
		my $owner = $bus->get_service_owner($name);
		$quiet or say $name, " ", $owner // "(none)";
		$owner or ++$err;
	}
	exit !!$err;
} else {
	usage();
	exit 2;
}

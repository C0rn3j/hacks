#!/usr/bin/env perl
use common::sense;
use Net::DBus;
use Getopt::Long qw(:config no_ignore_case bundling);

my $urgency	= 1;
my $icon	= "";
my $id		= 0;
my $summary	= "";
my $body	= "";
my @actions	= ();
my %hints	= ();
my $application	= "notify";
my $timeout	= 3;
my $statefile	= undef;
my $oldbody	= "";
my $appendbody	= 0;
my $urgency	= undef;

GetOptions(
	'a|appname=s'	=> \$application,
	'i|icon=s'	=> \$icon,
	'r|replace=i'	=> \$id,
	't|timeout=i'	=> \$timeout,
	's|state=s'	=> \$statefile,
	'A|append'	=> \$appendbody,
	'u|urgency=s'	=> \$urgency,
) or die "$@";

$summary = shift(@ARGV) or die "notify: summary not given\n";
$body = join(" ", @ARGV);

if (defined $statefile) {
	if (open(my $fh, "<", $statefile)) {
		$id = int <$fh>;
		chomp($oldbody = join("", <$fh>));
		close($fh);
	}
}

if ($appendbody) {
	unless (defined $statefile) {
		warn "notify: --append is useless without state file\n";
	}
	$body = join("\n", grep {length} ($oldbody, $body));
}

if (defined $urgency) {
	if ($urgency ~~ ['low', 'normal', 'critical']) {
		$hints{urgency} = $urgency;
	} else {
		die "notify: invalid urgency level given (can be low, normal, critical)\n";
	}
}

my $bus = Net::DBus->session;
my $svc = $bus->get_service("org.freedesktop.Notifications");
my $obj = $svc->get_object("/org/freedesktop/Notifications");

$id = $obj->Notify($application,
		$id,
		$icon,
		$summary,
		$body,
		\@actions,
		\%hints,
		$timeout);

if (defined $statefile) {
	open(my $fh, ">", $statefile) or die "notify: cannot update state: $@\n";
	say $fh $id;
	say $fh $body;
	close($fh);
} else {
	say $id;
}

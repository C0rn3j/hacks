#!/usr/bin/env perl
use feature "switch";

# this matches:
#   filename:lineno
#   filename:lineno:garbage
#   filename:lineno garbage
#   filename:/regex
#   filename:/regex garbage

my $specre = qr/^ ([^:]+) : (\d+ | \/[^\/]\S*) (?:[:\s].*)? $/mx;

# this matches:
#   ./path
#   ./path:garbage
#   /path
#   /path:garbage

my $pathre = qr/ (\.?\/[^:]+) /mx;

# this script is usually symlinked as ~/bin/vim, so
# it needs to run the real vim by absolute path

my $vim = "/usr/bin/vim";

my @args = "vim";

sub parse {
	for (shift) {
		when ($specre) {
			my ($file, $cmd) = ($1, $2);
			$file =~ s|^~/|$ENV{HOME}/|;
			$cmd =~ s|^|+|;
			return ($file, $cmd);
		}
		when ($pathre) {
			my ($file) = ($1);
			return ($file) if -e $file;
		}
		default {
			return;
		}
	}
	return;
}

if (@ARGV) {
	for my $arg (@ARGV) {
		if (my @r = parse($arg)) {
			push @args, @r;
		} else {
			push @args, $arg;
		}
	}
} else {
	if (my @r = parse(`xclip -o -s primary`)) {
		push @args, @r;
	}
}

exec {$vim} @args;
